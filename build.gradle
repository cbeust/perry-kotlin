import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    ext.kotlinVersion = '1.3.61'
    ext.dropWizardVersion = '1.3.9'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "com.heroku.sdk.heroku-gradle" version "1.0.1"
}

version '1.0-SNAPSHOT'
group 'com.beust.perry'
description 'Perry Rhodan wdb site'

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.heroku.sdk.heroku-gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
        "com.google.inject:guice:4.2.0",
        "com.hubspot.dropwizard:dropwizard-guice:1.0.6.0",
        "org.postgresql:postgresql:42.2.5.jre7",
        "org.jetbrains.exposed:exposed:0.12.1",
        "io.dropwizard:dropwizard-auth:$dropWizardVersion",
        "io.dropwizard:dropwizard-core:$dropWizardVersion",
        "io.dropwizard:dropwizard-assets:$dropWizardVersion",
        "io.dropwizard:dropwizard-views:$dropWizardVersion",
        "io.dropwizard:dropwizard-views-mustache:$dropWizardVersion",
        "javax.mail:mail:1.5.0-b01",
        "com.github.spullara.mustache.java:compiler:0.9.6",
        "org.twitter4j:twitter4j-stream:4.0.7",
        "org.jsoup:jsoup:1.11.3",
        "com.google.cloud:google-cloud-translate:1.71.0",
        "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2",
        "org.glassfish.jaxb:jaxb-runtime:2.3.2"
//    ,
//        "io.dropwizard.metrics:metrics-core:3.1.0",
//        "io.dropwizard.metrics:metrics-servlets:3.1.0"

    testCompile "org.testng:testng:6.14.3"
}

mainClassName = 'com.beust.perry.MainKt'
//def demoPort = System.getenv("PORT") ?: 80
//applicationDefaultJvmArgs = ["-Ddw.server.applicationConnectors[0].port=" + demoPort.toString()]

run {
    args = ['server', 'config.yml']
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
}

// Heroku

task stage(dependsOn: ['shadowJar', 'clean'])

task copyToLib(type: Copy) {
    into "$buildDir/lib"
    from(configurations.compile)
}

stage.dependsOn(copyToLib)

heroku {
    appName = 'perry-kotlin'
}